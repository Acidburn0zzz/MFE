(select tool CRC .)

(set include BOOL off .)
(set include TRUTH-VALUE on .)

 (fmod HF-SETS is
    sort Magma .
    sort Set .
    subsort Set < Magma .
    op _`,_ : Set Magma -> Magma [ctor assoc comm] .
    op _`,_ : Magma Magma -> Magma [assoc comm] .
    op `{_`} : Magma -> Set [ctor] .
    op `{`} : -> Set [ctor] .

    vars M M' N : Magma .
    vars S S' : Set .
    eq [01]: M, M, M' = M, M' .
    eq [02]: M, M = M .

    op _in_ : Magma Magma -> Bool .

    eq [03]: M in {} = false .
    eq [04]: {} in {{M}} = false .
    eq [05]: {} in {{}} = true .
    eq [06]: {} in {{}, M} = true .
    eq [07]: {} in {{M}, N} = {} in {N} .

    eq [08]: {M} in {M'} = M in M' .

   ceq [09]: S in S', N = true if S in S' = true .
   ceq [10]: S in S', N = S in N if S in S' = false .

   ceq [11]: S, M in M' = M in M' if S in M' = true .
   ceq [12]: S, M in M' = false if S in M' = false .

    op _equiv_ : Magma Magma -> Bool [comm] .
    eq [13]: M equiv M = true .
    eq [14]: {} equiv {M} = false .
    eq [15]: {M} equiv {M'} = M equiv M' .
    eq [16]: S equiv S, M' = S equiv M' .
   ceq [17]: S equiv M = false if S in {M} = false .
   ceq [18]: S, M equiv M' = false if S in {M'} = false .
   ceq [19]: M equiv M' = false if {M} in {{M'}} = false .
  endfm)

 (fmod HF-SETS-INC is
    protecting HF-SETS .

    op _<=_ : Magma Magma -> Bool .

    vars M M' : Magma .
    vars S S' : Set .

    eq [20]: {} <= M = true . 
   ceq [21]: {M} <= M' = true  if M in M' = true .
   ceq [22]: {M} <= M' = false if M in M' = false .
   ceq [23]: S, M <= M' = M <= M' if S <= M' = true .
   ceq [24]: S, M <= M' = false   if S <= M' = false .
  endfm)

---(
set print attribute on .
set trace on .
set trace select on .
trace select crcRemoveContextJoinableAndUnfeasibleCPs 
             ----delete 
             ----simplify 
             ----maximalCPSet 
             .
---)

 (check Church-Rosser HF-SETS-INC .)

---(
rewrites: 7634244 in 12439ms cpu (13862ms real) (613733 rewrites/second)

Church-Rosser checking of HF-SETS-INC
Checking solution:
The following critical pairs cannot be joined:
  ccp for 13 and 17
    true
    = false
    if S:Set in{S:Set}= false .
  ccp for 13 and 18
    true
    = false
    if S:Set in{M:Magma,S:Set}= false .
The specification is sort-decreasing.
---)
 
 (fmod HF-SETS-EXTRAS-1 is
    pr HF-SETS-INC .
    var S : Set .
    eq [25]: S in {S} = true .
  endfm) 

 (check Church-Rosser HF-SETS-EXTRAS-1 .)
 
---(
rewrites: 7731852 in 13057ms cpu (14713ms real) (592126 rewrites/second)

Church-Rosser checking of HF-SETS-EXTRAS-1
Checking solution:
The following critical pairs cannot be joined:
  cp for 08 and 25
    #1:Magma in{#1:Magma}
    = true .
  ccp for 13 and 18
    true
    = false
    if S:Set in{M:Magma,S:Set}= false .
The specification is sort-decreasing.
---)

 (fmod HF-SETS-EXTRAS-2 is
    pr HF-SETS-INC .
    var M : Magma .
    eq [25']: M in {M} = true .
  endfm)

 (check Church-Rosser HF-SETS-EXTRAS-2 .)

---(
rewrites: 7725698 in 13382ms cpu (15549ms real) (577280 rewrites/second)

Church-Rosser checking of HF-SETS-EXTRAS-2
Checking solution:
The following critical pairs cannot be joined:
  ccp for 11 and 25'
    #1:Magma in{#1:Magma,#2:Set}
    = true
    if #2:Set in{#1:Magma,#2:Set}= true .
  ccp for 12 and 25'
    false
    = true
    if #2:Set in{#1:Magma,#2:Set}= false .
The specification is sort-decreasing.
---)

 (fmod HF-SETS-EXTRAS-3 is
    pr HF-SETS-INC .
    vars M M' : Magma .
    eq [26]: M in {M, M'} = true .
  endfm) 

 (check Church-Rosser HF-SETS-EXTRAS-3 .)

---(
rewrites: 7996967 in 13898ms cpu (15839ms real) (575375 rewrites/second)

Church-Rosser checking of HF-SETS-EXTRAS-3
Checking solution:
The following critical pairs cannot be joined:
  cp for 02 and 26
    M':Magma in{M':Magma}
    = true .
  cp for 08 and 26
    #1:Magma in M':Magma,{#1:Magma}
    = true .
  ccp for 13 and 17
    true
    = false
    if S:Set in{S:Set}= false .
The specification is sort-decreasing.
---)

 (fmod HF-SETS-EXTRAS-4 is
    pr HF-SETS-INC .
    vars M M' : Magma .
    eq [25']: M in {M} = true .
    eq [26]: M in {M, M'} = true .
  endfm) 

 (check Church-Rosser HF-SETS-EXTRAS-4 .)

---(
rewrites: 8084433 in 14400ms cpu (16415ms real) (561385 rewrites/second)

Church-Rosser checking of HF-SETS-EXTRAS-4
Checking solution:
The following critical pairs cannot be joined:
  cp for 08 and 26
    #1:Magma in M':Magma,{#1:Magma}
    = true .
The specification is sort-decreasing.
---)

 (fmod HF-SETS-EXTRAS-5 is
    pr HF-SETS-INC .
    vars M M' : Magma .
    eq [25']: M in {M} = true .
    eq [26]: M in {M, M'} = true .
    eq [27]: M in {M}, M' = true .
  endfm) 

 (check Church-Rosser HF-SETS-EXTRAS-5 .)

---(
rewrites: 8243907 in 15002ms cpu (16880ms real) (549515 rewrites/second)

Church-Rosser checking of HF-SETS-EXTRAS-5
Checking solution:
The following critical pairs cannot be joined:
  ccp for 11 and 27
    #1:Magma in M':Magma,{#1:Magma,#2:Set}
    = true
    if #2:Set in M':Magma,{#1:Magma,#2:Set}= true .
The specification is sort-decreasing.
---)

  (fmod HF-SETS-EXTRAS-6 is
    pr HF-SETS-INC .
    vars M M' M'' : Magma .
    eq [25']: M in {M} = true .
    eq [26]: M in {M, M'} = true .
    eq [27]: M in {M}, M' = true .
    eq [28]: M in {M, M'}, M'' = true .
  endfm) 

 (check Church-Rosser HF-SETS-EXTRAS-6 .)
 
---(
rewrites: 8731166 in 16143ms cpu (18299ms real) (540833 rewrites/second)

Church-Rosser checking of HF-SETS-EXTRAS-6
Checking solution:
All critical pairs have been joined.
The specification is locally-confluent.
The specification is sort-decreasing.
---)

(show all critical pairs .)

(submit .)
(select tool MTT .)
(select external tool aprove .)

set trace off .
set print conceal on .
print reveal fmod_is_sorts_.____endfm 
             mod_is_sorts_._____endm
             .
set trace select on .
trace select terminationCheck 
             .

(ct .)

