
set include BOOL off .

fmod LATTICE is
  sort Lat .
  ops 0 1 : -> Lat .                       *** bottom and top
  op _\/_ : Lat Lat -> Lat [assoc comm prec 55] .  *** sup
  op _/\_ : Lat Lat -> Lat [assoc comm prec 59] .  *** inf
  op c :  Lat -> Lat .                     *** complement

  vars X Y Z : Lat .

  *** idempotency
  eq [LAT-01] : X \/ X = X .
  eq [LAT-02] : X /\ X = X .
  *** extensions
  eq [LAT-03] : Y \/ X \/ X = Y \/ X .
  eq [LAT-04] : Y /\ X /\ X = Y /\ X .

  *** identities
  eq [LAT-05] : X \/ 0 = X .
  eq [LAT-06] : X /\ 1 = X .
  
  *** simple absorption (follows from identities and absorption)
  eq [LAT-07] : 1 \/ X = 1 .
  eq [LAT-08] : 0 /\ X = 0 .
  *** absorption
  eq [LAT-09] : X /\ (X \/ Y) = X .
  eq [LAT-10] : X \/ (X /\ Y) = X .
  *** extensions
  eq [LAT-11] : Z /\ X /\ (X \/ Y) = Z /\ X .
  eq [LAT-12] : Z \/ X \/ (X /\ Y) = Z \/ X .

  *** modular lattice axiom
 ceq [LAT-13] : Z /\ (X \/ Y) = X \/ (Y /\ Z) if (X /\ Z) = X .

  *** complement axioms
  eq [LAT-14] : X \/ c(X) = 1 .
  eq [LAT-15] : X /\ c(X) = 0 .
  *** extensions
  eq [LAT-16] : Z \/ X \/ c(X) = 1 .
  eq [LAT-17] : Z /\ X /\ c(X) = 0 .
  ***
  eq [LAT-18] : c(0) = 1 .
  eq [LAT-19] : c(1) = 0 .
  ***
  eq [LAT-20] : c(X /\ Y) \/ X = 1 .
  eq [LAT-21] : c(X \/ Y) /\ X = 0 .
  *** extensions
  eq [LAT-22] : c(X /\ Y) \/ X \/ Z = 1 .
  eq [LAT-23] : c(X \/ Y) /\ X /\ Z = 0 .
endfm


